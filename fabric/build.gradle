plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    // Mod Menu
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}")
    implementation(include("folk.sisby:kaleido-config:${kaleido_version}"))
    modImplementation "maven.modrinth:mcqoy:adCKjC4q"

    modImplementation "maven.modrinth:compost:${compost_version}"
    modImplementation "cc.cassian.item-descriptions:item-descriptions-fabric:${item_descriptions_version}+${minecraft_version}"
    modImplementation("vectorwing:FarmersDelight:${fdrf_version}+refabricated") {
        exclude(group: "net.fabricmc")
        exclude(group: "me.shedaniel")
    }
    modApi("fuzs.iteminteractions:iteminteractions-fabric:$iteminteractions_version")

    // Developer tools
    modRuntimeOnly "maven.modrinth:eiv:${eiv_version}-fabric"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:$rei_version"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin:$rei_version"
//    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
    modImplementation "maven.modrinth:ferrite-core:CtMpt7Jr"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
    from(rootProject.file("common/src/main/resources")) {
        include("assets/bigger_fish/icon.png")
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
